@top Mips {
  Instr*
  expression*
}

@skip { space | LineComment | "," }

expression {
  Macro |
  LabelGroup { Label Instr* }
}

Instr {
  Op args+
}

args {
  Register |
  Identifier { identifier } |
  String |
  Number
}


@external specialize {identifier} specializeIdent from "./tokenizer" {
  Label,
  Op
}

@tokens {
  identifier { $[a-zA-Z_] $[a-zA-Z_\-0-9]* ":"? }

  String { ('"' (!["\\] | "\\" _)* '"') | ("'" (!['\\] | '\\' _)* "'") }

  Register { "$" ( $[a-z0-9]+ ) }

  Number { ("0x" $[0-9a-fA-F]+) | "-"? $[0-9]+ | ("0b" $[01]+)}

  LineComment { "#" ![\n]* }

  space { $[ \t\n\r]+ }

  Macro { "." $[a-zA-Z_] $[a-zA-Z_\-0-9]* }

  ","
}

@detectDelim
